= 16. Functor

== 16.4 Let's talk about f, baby

=== Exercises: Be Kind

1. The kind of `a -> a` is `*`
2. The kind of `b` and `T` is `* -> *`
3. The kind of `c` is `* -> * -> *`

== 16.7 Commonly used functors

=== Exercises: Heavy Lifting

.1.
[source, haskell]
a = fmap (+1) $ read "[1]" :: [Int]

.2. 
[source, haskell]
b = (fmap . fmap) (++"lol") (Just ["Hi,", "Hello"])

.3.
[source, haskell]
c = (*2) . (\x -> x - 2)

.4.
[source, haskell]
d = ((return '1' ++) . show) . (\x -> [x, 1..3])

.5.
[source, haskell]
----
{-- first pass
e :: IO Integer
e =
  let
    ioi = readIO "1" :: IO Integer
    changed = (fmap . fmap) (:[]) . fmap (("123"++) . show) $ ioi
    r = (fmap . fmap) ((*3) . read) changed
  in
    fmap (read . concat) $ (fmap . fmap) show r
--}

(|>) :: a -> (a -> b) -> b
(|>) a f = f a

e :: IO Integer
e =
  (readIO "1" :: IO Integer)
  |> fmap (:[]) -- fmap singleton
  |> fmap ([1..3] ++)
  |> (fmap . fmap) (*3)
  |> (fmap. fmap) show
  |> fmap concat
  |> fmap read
----

== 16.10 Exercises: Instances of Func

.1.
[source, haskell]
----
instance Functor Identity where
  fmap f (Identity x) = Identity (f x)

λ> quickCheck (functorIdentity :: Identity Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Identity Int))
+++ OK, passed 100 tests.
----

.2.
[source, haskell]
----
instance Functor Pair where
  fmap f (Pair x y) = Pair (f x) (f y)

λ> quickCheck (functorIdentity :: Pair Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Pair Int))
+++ OK, passed 100 tests.
----

.3.
[source, haskell]
----
instance Functor (Two a) where
  fmap f (Two x y) = Two x (f y)

λ> quickCheck (functorIdentity :: Two String Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Two String Int))
+++ OK, passed 100 tests.
----

.4.
[source, haskell]
----
instance Functor (Three a b) where
  fmap f (Three x y z) = Three x y (f z)

λ>  quickCheck (functorIdentity :: Three String String Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Three String String Int))
+++ OK, passed 100 tests.
----

.5.
[source, haskell]
----
instance Functor (Three' a) where
  fmap f (Three' x y z) = Three' x (f y) (f z)

λ> quickCheck (functorIdentity :: Three' String Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Three' String Int))
+++ OK, passed 100 tests.
----

.6.
[source, haskell]
instance Functor (Four a b c) where
  fmap f (Four a b c d) = Four a b c (f d)

.7.
[source, haskell]
instance Functor (Four' a) where
  fmap f (Four' a b c d) = Four' a b c (f d)

.8.
----
No, the type `Trivial` does not have kind * -> *.
----

== 16.11 Ignoring Possibilities

=== Exercise: Possibly

[source, haskell]
----
instance Functor Possibly where
  fmap _ LolNope = LolNope
  fmap f (Yeppers x) = Yeppers (f x)

λ> fmap (+1) LolNope
LolNope
λ> fmap (+1) (Yeppers 1)
Yeppers 2
----

=== Short Exercise

.1.
[source, haskell]
----
instance Functor (Sum a) where
  fmap _ (First x) = (First x)
  fmap f (Second x) = Second (f x)

λ> fmap (+1) (First 1)
First 1
λ> fmap (+1) (Second 1)
Second 2
----

.2.
----
`fmap` isn't possibile to `First` because the `a` type variable doesn't participate in the Functor definition (in order to satisfy * -> *)
----

== 16.17 Chapter Exercises

=== Can a valid Functor be written?

1. No. Kind is *
2. Yes. Kind is * -> *
3. Yes. Kind is * -> *
4. No. Kind is (* -> *) -> *
5. No. Kind is *   

=== Rearrange the arguments

.1.
[source, haskell]
data Sum' a b
  = First' b
  | Second' a

.2.
[source, haskell]
data Company a b c
  = DeepBlue a b
  | Something c

.3.
[source, haskell]
----
data More a b
  = L b a b
  | R a b a

λ> fmap (+1) (L 1 2 3)
L 2 2 4
λ> fmap (+1) (R 1 2 3)
R 1 3 3
----

=== Instances

.1.
[source, haskell]
----
data Quant a b
  = Finance
  | Desk a
  | Bloor b
  deriving (Eq, Show)

instance Functor (Quant a) where
  fmap _ Finance = Finance
  fmap _ (Desk x) = Desk x
  fmap f (Bloor x) = Bloor (f x)

instance (Arbitrary a, Arbitrary b) => Arbitrary (Quant a b) where
  arbitrary =
    oneof [ pure Finance
          , pure Desk <*> arbitrary
          , pure Bloor <*> arbitrary
          ]

λ> quickCheck (functorIdentity :: Quant String Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Quant String Int))
+++ OK, passed 100 tests.
----

.2.
[source, haskell]
----
data K a b
  = K a
  deriving (Eq, Show)

instance Functor (K a) where
  fmap f (K x) = K x

instance (Arbitrary a, Arbitrary b) => Arbitrary (K a b) where
  arbitrary = pure K <*> arbitrary

λ> quickCheck (functorIdentity :: K String Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: K String Int))
+++ OK, passed 100 tests.
----

.3.
[source, haskell]
----
newtype Flip f a b
  = Flip (f b a)
  deriving (Eq, Show)

instance Functor (Flip K a) where
  fmap f (Flip (K x)) = Flip $ K (f x)

instance (Arbitrary a, Arbitrary b) => Arbitrary (Flip K a b) where
  arbitrary = pure Flip <*> arbitrary

λ> quickCheck (functorIdentity :: Flip K Int Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Flip K Int Int))
+++ OK, passed 100 tests.
----

.4.
[source, haskell]
----
data EvilGoateeConst a b
  = GoatyConst b
  deriving (Eq, Show)

instance Functor (EvilGoateeConst a) where
  fmap f (GoatyConst x) = GoatyConst (f x)

instance (Arbitrary a, Arbitrary b) => Arbitrary (EvilGoateeConst a b) where
  arbitrary = pure GoatyConst <*> arbitrary

λ> quickCheck (functorIdentity :: EvilGoateeConst String Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: EvilGoateeConst String Int))
+++ OK, passed 100 tests.
----

.5.
[source, haskell]
----
data LiftItOut f a
  = LiftItOut (f a)
  deriving (Eq, Show)

instance Functor f => Functor (LiftItOut f) where
  fmap f (LiftItOut fa) = LiftItOut (fmap f fa)

-- I have no idea what I'm doing. Why does (f a) work?
-- Figured it out reading compiler message, not sure what
-- the differnce between (f a) and f a is
instance (Arbitrary (f a)) => Arbitrary (LiftItOut f a) where
  arbitrary = pure LiftItOut <*> arbitrary

λ> quickCheck (functorIdentity :: LiftItOut Maybe Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: LiftItOut Maybe Int))
+++ OK, passed 100 tests.
----

.6.
[source, haskell]
----
data Parappa f g a
  = DaWrappa (f a) (g a)
  deriving (Eq, Show)

instance (Functor f, Functor g) => Functor (Parappa f g) where
  fmap f (DaWrappa (fa) (ga)) = DaWrappa (fmap f fa) (fmap f ga)

instance (Arbitrary (f a), Arbitrary (g a)) => Arbitrary (Parappa f g a) where
  arbitrary = pure DaWrappa <*> arbitrary <*> arbitrary

λ> quickCheck (functorIdentity :: Parappa (Either String) Maybe Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Parappa (Either String) Maybe Int))
+++ OK, passed 100 tests.
----

.7.
[source, haskell]
----
data IgnoreOne f g a b
  = IgnoringSomething (f a) (g b)
  deriving (Eq, Show)

instance Functor g => Functor (IgnoreOne f g a) where
  fmap f (IgnoringSomething (fa) (gb)) = IgnoringSomething (fa) (fmap f gb)

-- Woah. Now I'm even more weirded out.
-- I guess it knows about the combos f a, g b from the type constructor?
instance (Arbitrary (f a), Arbitrary (g b)) => Arbitrary (IgnoreOne f g a b) where
  arbitrary = pure IgnoringSomething <*> arbitrary <*> arbitrary

λ> quickCheck (functorIdentity :: IgnoreOne Maybe (Either String) Int Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: IgnoreOne [] Maybe String Int))
+++ OK, passed 100 tests.
----

.8.
[source, haskell]
----
data Notorious g o a t
  = Notorious (g o) (g a) (g t)
  deriving (Eq, Show)

instance Functor g => Functor (Notorious g o a) where
  fmap f (Notorious (go) (ga) (gt)) =
    Notorious go ga (fmap f gt)

instance ( Arbitrary (g o)
         , Arbitrary (g a)
         , Arbitrary (g t)
         ) => Arbitrary (Notorious g o a t) where
  arbitrary = pure Notorious <*> arbitrary <*> arbitrary <*> arbitrary

λ> quickCheck (functorIdentity :: Notorious Maybe String Int Int-> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: Notorious [] Int Int Int))
+++ OK, passed 100 tests.
----

.9.
[source, haskell]
----
data List a
  = Nil
  | Cons a (List a)
  deriving (Eq, Show)

instance Functor List where
  fmap _ Nil = Nil
  fmap f (Cons x xs) = Cons (f x) (fmap f xs)

instance Arbitrary a => Arbitrary (List a) where
  arbitrary =
    let
      arbitraryList :: (Arbitrary a) => Int -> Gen (List a)
      arbitraryList s
        | s == 0 = pure Nil
        | s > 10 = arbitraryList 10
        | otherwise = pure Cons <*> arbitrary <*> arbitraryList (s - 1)
    in
      sized arbitraryList

λ> quickCheck (functorIdentity :: List Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: List Int))
+++ OK, passed 100 tests.
----

.10.
[source, haskell]
----
data GoatLord a
  = NoGoat
  | OneGoat a
  | MoreGoats (GoatLord a) (GoatLord a) (GoatLord a)
  deriving (Eq, Show)

instance Functor GoatLord where
  fmap _ NoGoat = NoGoat
  fmap f (OneGoat x) = OneGoat (f x)
  fmap f (MoreGoats x y z) = MoreGoats (fmap f x) (fmap f y) (fmap f z)

instance Arbitrary a => Arbitrary (GoatLord a) where
  arbitrary = oneof [ pure NoGoat
                    , pure OneGoat <*> arbitrary
                    , pure MoreGoats <*> arbitrary <*> arbitrary <*> arbitrary
                    ]

λ> quickCheck (functorIdentity :: GoatLord Int -> Bool)
+++ OK, passed 100 tests.
λ> quickCheck (\x -> functorCompose (+1) (+2) (x :: GoatLord Int))
+++ OK, passed 100 tests.
----

.11.
[source, haskell]
----
data TalkToMe a
  = Halt
  | Print String a
  | Read (String -> a)

instance Functor TalkToMe where
  fmap _ Halt = Halt
  fmap f (Print str x) = Print str (f x)
  fmap f (Read g) = Read (f . g)

-- I still haven't figured out how to test when type can't derive Eq
----