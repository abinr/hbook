= 24. Parser combinators

== 24.2 A few more words of introduction

=== Exercises: Parsing Practice

.1.
[source, haskell]
----
testParse :: Parser Char -> IO ()
testParse p =
  print $ parseString (p >> eof) mempty "123"

one:
Failure (ErrInfo {_errDoc = (interactive):1:2: error: expected: end of input
123<EOF> 
 ^       , _errDeltas = [Columns 1 1]})

oneTwo:
Failure (ErrInfo {_errDoc = (interactive):1:3: error: expected: end of input
123<EOF> 
  ^      , _errDeltas = [Columns 2 2]})
----

.2.
[source, haskell]
----
-- had to look ahead a couple sections for (<|>)
choose123 :: Parser String
choose123 =
  string "123" <|> string "12" <|> string "1"

parseString choose123 mempty "1"
Success "1"
parseString choose123 mempty "12"
Success "12"
parseString choose123 mempty "123"
Success "123"
----

.3.
[source, haskell]
----
str :: CharParsing m => String -> m String
str s = traverse char s

parseString (str "1") mempty "123"
Success "1"
parseString (str "12") mempty "123"
Success "12"
parseString (str "123") mempty "123"
Success "123"
----

== 24.4 Parsing Fractions

=== Exercise: Unit of Success

[source, haskell]
----
u :: Parser Integer
u =
  pure const <*> integer <*> eof

parseString (u) mempty "123"
Success 123
parseString (u) mempty "123abc"
Failure (ErrInfo {_errDoc = (interactive):1:4: error: expected: digit,
    end of input
123abc<EOF> 
   ^        , _errDeltas = [Columns 3 3]})
----

== 24.6 Alternative

=== Exercise: Try Try

[source, haskell]
----
main :: IO ()
main = do
  let parseFraction' =
        parseString dof mempty
  ...
  print $ parseFraction' shouldAlsoWork
  print $ parseFraction' "0.0"
  print $ parseFraction' "3.14"
  print $ parseFraction' ".03"

parseDecimal :: Parser Rational
parseDecimal = fmap toRational double

dof :: Parser Rational
dof = try parseFraction <|> parseDecimal

main
...
Success (2 % 1)
Success (0 % 1)
Success (7070651414971679 % 2251799813685248)
Failure (ErrInfo {_errDoc = (interactive):1:1: error: expected: digit,
    double
.03<EOF> 
^        , _errDeltas = [Columns 0 0]})

----

== 24.11 Chapter Exercises

=== 1. SemVer

[source, haskell]
----
module SemVer where

import Text.Trifecta
import Control.Applicative
import Data.Semigroup
import Numeric.Natural
import Data.Char (digitToInt)

data SemVer
  = SemVer Major Minor Patch Release Metadata
  deriving (Eq, Show)

-- Tests pass so far but this implementation seems overcomplex
instance Ord SemVer where
  compare (SemVer major minor patch rel _) (SemVer major' minor' patch' rel' _) =
    let
      a = listToInteger . fmap toInteger $ [major, minor, patch]
      b = listToInteger . fmap toInteger $ [major', minor', patch']
    in
      case a `compare` b of
        GT -> GT
        LT -> LT
        EQ ->
          case (rel, rel') of
            ([], []) -> EQ
            ([], _) -> GT
            (_, []) -> LT
            (_, _) ->
              let
                x = fmap (uncurry compare) $ zip rel rel'
                y = compare (length rel) (length rel')
              in
                mconcat x <> y


type Major = Natural
type Minor = Natural
type Patch = Natural
type Release = [NumberOrString]
type Metadata = [String]

data NumberOrString
  = NOSS String
  | NOSI Natural
  deriving (Eq, Show)

instance Ord NumberOrString where
  compare (NOSS s) (NOSS s') = compare s s'
  compare (NOSI n) (NOSI n') = compare n n'
  compare (NOSS s) (NOSI n)  = GT
  compare (NOSI n) (NOSS s)  = LT


parseSemVer :: Parser SemVer
parseSemVer = do
  major <- strictNatural
  char '.'
  minor <- strictNatural
  char '.'
  patch <- strictNatural
  optional (char '-')
  releases <- parseNumberOrString `sepEndBy` (symbol ".")
  optional (char '+')
  metadata <- (some $ alphaNum <|> char '-') `sepEndBy` (symbol ".")
  pure $ SemVer major minor patch releases metadata

parseNumberOrString :: Parser NumberOrString
parseNumberOrString =
  NOSI . fromInteger <$> natural <|> NOSS <$> some (alphaNum <|> char '-')

-- matches any natural number, leading zeros not allowed
strictNatural :: Parser Natural
strictNatural =
  loneZero <|> fmap (fromInteger . listToInteger) ints

loneZero :: Parser Natural
loneZero = (char '0' <* notFollowedBy alphaNum) *> pure 0

naturals :: Parser String
naturals = pure (:) <*> (satisfyRange '1' '9') <*> (many digit)

ints :: Parser [Integer]
ints = (fmap . fmap) (toInteger . digitToInt) naturals

listToInteger :: [Integer] -> Integer
listToInteger xs = foldl1 (\acc a -> acc * 10 + a) xs
----

=== 2. Positive Integers

[source, haskell]
----
module PositiveIntegers where

import Text.Trifecta
import qualified Data.Char as Char

parseDigit :: Parser Char
parseDigit = satisfyRange '0' '9' <?> "parseDigit"

base10Integer :: Parser Integer
base10Integer =
  fmap listToInteger $ (fmap . fmap) (toInteger . Char.digitToInt) ints

ints :: Parser String
ints =  pure (:) <*> parseDigit <*> many parseDigit

listToInteger :: [Integer] -> Integer
listToInteger xs = foldl (\acc a -> acc * 10 + a) 0 xs

λ> parseString parseDigit mempty "123"
Success '1'
λ> parseString parseDigit mempty "abc"
Failure (ErrInfo {_errDoc = (interactive):1:1: error: expected: parseDigit
abc<EOF> 
^        , _errDeltas = [Columns 0 0]})
λ> parseString base10Integer mempty "123abc"
Success 123
λ> parseString base10Integer mempty "abc"
Failure (ErrInfo {_errDoc = (interactive):1:1: error: expected: parseDigit
abc<EOF> 
^        , _errDeltas = [Columns 0 0]})
----

=== 3. Negative Integers

[source, haskell]
----
module PositiveIntegers where

import Text.Trifecta
import Control.Applicative
import qualified Data.Char as Char
import Data.Function

base10Integer :: Parser Integer
base10Integer =
  fmap digitsToInteger (some parseDigit)

base10Integer' :: Parser Integer
base10Integer' = do
  sign <- optional $ char '-'
  num <- base10Integer
  case sign of
    Just '-' -> return $ negate num
    Nothing -> return num

digitsToInteger :: String -> Integer
digitsToInteger = listToInteger . fmap (toInteger . Char.digitToInt)

parseDigit :: Parser Char
parseDigit = satisfyRange '0' '9' <?> "parseDigit"

listToInteger :: [Integer] -> Integer
listToInteger xs = foldl (\acc a -> acc * 10 + a) 0 xs

λ> parseString base10Integer' mempty "-123abc"
Success (-123)
----

=== 4. Phone Number

[source, haskell]
----
module PhoneParser where

import Text.Trifecta
import Control.Applicative

type Area = String
type Exchange = String
type LineNumber = String

data PhoneNumber =
  PhoneNumber Area Exchange LineNumber
  deriving (Eq, Show)

parsePhone :: Parser PhoneNumber
parsePhone = do
  optional $ string "1-"
  optional $ char '('
  area <- count 3 digit
  optional $ char ')'
  optional space
  optional $ char '-'
  exchg <- count 3 digit
  optional $ char '-'
  line <- count 4 digit
  return $ PhoneNumber area exchg line
----

